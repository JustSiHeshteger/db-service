plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'checkstyle'
    id 'org.liquibase.gradle' version '2.2.1'
}

group = 'ru.zvrg'
version = getVersion()

java {
    sourceCompatibility = '17'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources', 'src/main/liquibase']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //liquibase
    liquibaseRuntime 'org.liquibase:liquibase-core:3.8.9'
    liquibaseRuntime("ch.qos.logback:logback-core:1.2.3")
    liquibaseRuntime("ch.qos.logback:logback-classic:1.2.3")
    liquibaseRuntime 'org.postgresql:postgresql'
    liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
    liquibaseRuntime 'org.springframework.data:spring-data-jpa'
    liquibaseRuntime 'org.springframework.boot:spring-boot'
    liquibaseRuntime sourceSets.main.output
    liquibaseRuntime 'org.yaml:snakeyaml:1.26'

    implementation "org.telegram:telegrambots:${TELEGRAMBOTS_VERSION}"
    implementation "com.google.code.gson:gson:${GSON_VERSION}"
    implementation "com.puppycrawl.tools:checkstyle:${CHECKSTYLE_VERSION}"
    implementation "org.liquibase:liquibase-core:${LIQUIBASE_VERSION}"
}

ext {
    database = new Properties().with {
        load(file("src/main/resources/db.properties").newReader())
        it
    }
}

liquibase {
    activities {
        main {
            changeLogFile "src/main/liquibase/root-changelog.yaml"
            url database.getProperty("db.url")
            username database.getProperty("db.user")
            password database.getProperty("db.password")
        }
        runList = "main"
    }
}

checkstyle {
    configFile = file("src/main/resources/checkstyle.xml")
    showViolations = true
}

tasks.named('test') {
    useJUnitPlatform()
}

def getVersion() {
    return "${MAJOR}.${MINOR}.${PATCH}"
}